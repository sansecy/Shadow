import cn.migu.gamehall.shadow.coding.code_generator.ActivityCodeGenerator
import cn.migu.gamehall.shadow.coding.common_jar_settings.AndroidJar

buildscript {
    dependencies {
        classpath 'cn.migu.gamehall.shadow.coding:android-jar'
        classpath 'cn.migu.gamehall.shadow.coding:code-generator'
    }
}

apply plugin: 'cn.migu.gamehall.shadow.internal.common-jar-settings'

apply plugin: 'kotlin'

java {
    sourceSets {
        main.java.srcDirs += 'build/generated/sources/code-generator'
    }
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    kotlinOptions {
        jvmTarget = "1.8"
        noJdk = true
        noStdlib = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    implementation 'cn.migu.gamehall.shadow.coding:java-build-config'
    api project(':runtime')
    compileOnly project(':activity-container')
    compileOnly project(':common')
    api project(':load-parameters')
    compileOnly files(AndroidJar.ANDROID_JAR_PATH)
}

def generateCode = tasks.register('generateCode') {
    def outputDir = layout.buildDirectory.dir('generated/sources/code-generator')
    outputs.dir(outputDir)
            .withPropertyName('outputDir')
    doLast {
        ActivityCodeGenerator codeGenerator = new ActivityCodeGenerator()
        codeGenerator.generate(outputDir.get().getAsFile(), "loader")
    }
}

compileJava.dependsOn(generateCode)
compileKotlin.dependsOn(generateCode)
